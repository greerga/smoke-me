#!/usr/bin/perl
use 5.010;
use strict;
use warnings;
use constant {'true' => 1, 'false' => 0};

use FindBin      qw[$Bin $Script];
use File::Copy   qw[move];
use File::Find   qw[find];
use File::Temp   qw[tempfile];
use List::Util   qw[minstr];
use IPC::Open3   qw[open3];
use Getopt::Long qw[GetOptions];
use File::Path   qw[remove_tree];

sub msay(@);
sub nsay(@);

# Be really 'nice'.
eval { setpriority 0, 0, 20 };

# -----

my ($git, $perl, $rsync, $ssh, $workdir, $cfgtmpl, $cfg, $smokepl, $smoker, $log_dst, @cmd, $cmz_bin);

my %o = (
    'pattern'     => undef,
    'what'        => "smoke-me",
    'sleep'       => 60*5,  # seconds
    'remote'      => undef,
);
GetOptions(\%o, "pattern=s", "what=s", "sleep=s")
    or die "Unknown otions: @ARGV";

# Set defaults and compile patterns.
if (defined $o{'pattern'} and not defined $o{'remote'}) {
    ($o{'remote'}) = $o{'pattern'} =~ m{\bremotes/([^/]+)}
        or die "Unable to determine remote from pattern: $o{'pattern'}\n";
}
$o{'remote'}  //= "origin";
$o{'pattern'} //= "^remotes/$o{'remote'}/smoke-me/";
$o{'pattern'} = qr/$o{'pattern'}/;

# Configuration for installation location.
if ($^O eq "MSWin32") {
    require Storable;
    $ENV{'HOME'} = "d:/smoke";
    $git     = q{"c:/cygwin/bin/git.exe"};
    $perl    = q{"c:/perl/bin/perl.exe"};
    $rsync   = q{"c:/program files/cwrsync/bin/rsync.exe"};
    $ssh     = q{"'c:/program files/cwrsync/bin/ssh.exe'"};
    $cmz_bin = q{"c:/cygwin/bin/gzip.exe"};
    $workdir = "d:/smoke/perl/$o{'what'}";
    $cfg     = "d:/smoke/driver/$o{'what'}_smoke.config";
    $smokepl = "d:/smoke/driver/$o{'what'}_smoke.cmd";
    $smoker  = "d:/smoke/driver";
    $log_dst = 'perl@m-l.org:www/smoke/perl/win32';
    @cmd     = ("start", "/wait", "cmd", "/c", $smokepl);
#    @cmd     = ($smokepl);
} else {
    $git     = ("/usr/bin/git");
    $perl    = "/usr/bin/perl";
    $rsync   = "/usr/bin/rsync";
    $ssh     = "/usr/bin/ssh";
    $cmz_bin = "/bin/gzip";
    $workdir = "$ENV{HOME}/smoke/perl/$o{'what'}";
    $cfg     = "$ENV{HOME}/smoke/driver/$o{'what'}.config";
    $smokepl = "$ENV{HOME}/smoke/driver/$o{'what'}.sh";
    $smoker  = "$ENV{HOME}/smoke/driver";
    $log_dst = 'perl@m-l.org:www/smoke/perl/linux';
    @cmd     = ($smokepl);
}

$cfgtmpl = $cfg . ".template";

my $cmz_opt    = "-9f";
my $worksrcdir = "$workdir/source";
my $workintdir = "$workdir/inter";
my $workblddir = "$workdir/build";
my $srcdir     = "$ENV{HOME}/smoke/git/perl";
my $log        = "$o{'what'}-mesmoke.log";

# -----

if (! -d $worksrcdir) {
    system $rsync, "-a", $srcdir, $worksrcdir;
    die "rsync -a $srcdir $worksrcdir: $?"
        if $?;
} elsif (! -d "$worksrcdir/.git") {
    # Might have exited during smoking while .git/ was moved away.
    if (-d "$workdir/.git") {
        move "$workdir/.git", "$worksrcdir/.git"
            or die "$workdir/.git -> $worksrcdir/.git: move: $!";
    } else {
        die "Missing $worksrcdir/.git\n";
    }
}

chdir $worksrcdir
    or die "$worksrcdir: chdir: $!";

my $fh;
for (;;) {
    # Begin remote section.

    # Switch to a "neutral" branch because "branch -D" fails on the current one.
    # git checkout
    system $git, "checkout", "-q", "-f", "$o{'remote'}/blead";
    die "git checkout: $?"
        if $?;

    # git fetch
    system $git, "fetch", "-q", "--prune", $o{'remote'};
    warn "git fetch: $?"
        if $?;

    # Begin local section.

    # git branch
    my %branch;
    open $fh, "$git branch -av |"
        or die "git branch: open: $?";
    while (<$fh>) {
        chomp;
        if (!/^\*?\s*(?<name>\S+)\s+(?<sha>\S+)\s+(?<subject>.*)$/) {
            warn "'git branch' speaking in tongues? $_\n";
            next;
        }
        next if $+{sha} eq "->";  # references
        $branch{$+{name}} = $+{sha};
    }
    close $fh
        or die "git branch: close: $!";

    # Which remote branches don't match our local?
    my @remote_branches = sort map { s{^remotes/\Q$o{'remote'}\E/}{} ? $_ : () } grep { $_ =~ $o{'pattern'} } keys %branch;
    my @candidates = grep { !exists $branch{$_} || $branch{"remotes/$o{'remote'}/$_"} ne $branch{$_} } @remote_branches;
    if (@candidates == 0) {
        # Create list of remote branches for status display.
        my $remote_branches = "Remote branches:\n";
        foreach my $branch (sort @remote_branches) {
            my $active = !exists $branch{$branch}
                      || $branch{"remotes/$o{'remote'}/$branch"} ne $branch{$branch}
                        ? "... "
                        : "    "
                        ;
            $remote_branches .= join "", $active, $branch{"remotes/$o{'remote'}/$branch"}, "\t", $branch, "\n";
        }

        # Update status as sleeping.
        my ($status_fh, $status_board) = tempfile();
        print $status_fh scalar localtime, " | No candidates; sleeping.\n\n", $remote_branches
            or warn "status: print: $!";
        close $status_fh
            or warn "status: close: $!";
        chmod 0644, $status_board
            or warn "status: chmod: $!";
        (my $rsync_status_board = $status_board) =~ s{^(\w):[\\/]}{/cygdrive/$1/};
        system $rsync, "-e", $ssh, "-z", $rsync_status_board, "$log_dst/$o{'what'}/status";
        warn "rsync: $?"
            if $?;
        unlink $status_board
            or warn "status: unlink: $!";

        # Nighty-night.
        sleep $o{'sleep'};
        next;
    }

    # git log
    my @age;
    foreach my $name (@candidates) {
        my $sha = $branch{"remotes/$o{'remote'}/$name"}
            or die;
        # %ci = commit date, ISO 8601 format
        # %H  = commit hash
        # %cn = committer name
        # %ce = committer email
        # %n  = \n
        open $fh, qq{$git log -1 --pretty=format:"ci=%ci|H=%H|cn=%cn|ce=%ce%n" $sha |}
            or die "git log: open: $?";
        while (<$fh>) {
            chomp;
            my %data = map { /^([^=]+)=(.*)$/ } split /\|/;
            if (keys %data == 0) {
                warn "'git log' speaking in tongues? $_\n";
                next;
            }
            $data{'branch'} = $name;
            push @age, \%data;
        }
        close $fh
            or warn "git log: close($sha): $!";
    }

    # Open a file to use a status board viewable on a web site.
    my ($status_fh, $status_board) = tempfile();

    # Find the oldest SHA as the winner.
    @age = sort { $a->{'ci'} cmp $b->{'ci'} } @age;
    nsay "Smoke order:";
    say $status_fh "Smoke order:";
    foreach my $item (@age) {
        my $datetime = $item->{'ci'};
        $datetime =~ s/ [+-]\d{4}$//;

        my $line = "  $datetime $item->{'branch'} -- $item->{'cn'} <$item->{'ce'}>";
        nsay $line;
        say $status_fh $line;
    }
    my $winner = $age[0];

    # Publish "status" board on the web site for people to review.
    close $status_fh
        or warn "status: close: $!";
    chmod 0644, $status_board
        or warn "status: chmod: $!";
    (my $rsync_status_board = $status_board) =~ s{^(\w):[\\/]}{/cygdrive/$1/};
    system $rsync, "-e", $ssh, "-z", $rsync_status_board, "$log_dst/$o{'what'}/status";
    warn "rsync: $?"
        if $?;
    unlink $status_board
        or warn "$status_board: unlink: $!";

    # This is likely not needed anymore with the "checkout -f".
    # git clean
    system $git, "clean", "-dxqf";
    die "git clean: $?"
        if $?;

    # git branch
    if (exists $branch{$winner->{'branch'}}) {
        system $git, "branch", "-D", $winner->{'branch'};
        die "git branch: $?"
            if $?;
    }

    # git gc
    system $git, "gc", "--auto", "--quiet";
    die "git gc: $?"
        if $?;

    # git checkout
    system $git, "checkout", "-q", "-f", $winner->{'branch'};
    die "git checkout: $?"
        if $?;

    # This may be no longer necessary since 'branch -D/checkout' starts clean.
    # But we do it anyway in case 'git branch -D' fails miserably. It is
    # better to hallucinate up-to-date than fail to update and spam reports.
    # git pull
    system $git, "pull", "-q", $o{'remote'}, $winner->{'branch'};
    die "git pull: $?"
        if $?;

    # Create .patch for Test::Smoke.
    my $describe = `$git describe`;
    chomp $describe;
    open my $dotpatch, ">", ".patch"
        or die ".patch: open(>): $!";
    {
        my $dotpatch_iso8601 = $winner->{'ci'};
        for ($dotpatch_iso8601) {
            s/\s*[+-]\d{4}$//;
            tr/ /./;
        }
        say $dotpatch join " ", $winner->{'branch'}, $dotpatch_iso8601, $winner->{'H'}, $describe;
    }
    close $dotpatch
        or warn ".patch: close: $!";

    # Hide .git from Test::Smoke
    move "$worksrcdir/.git", "$workdir/.git"
        or die "$worksrcdir/.git -> $workdir/.git: move: $!";

    # Delete all the .gitignore files as they're not in MANIFEST.
    find({'wanted' => \&remove_gitignore, 'no_chdir' => true}, $worksrcdir);

    # Nuke intermediate and build directories to avoid contamination.
    foreach my $rmtree_dir ($workblddir, $workintdir) {
        remove_tree $rmtree_dir
            or die "$rmtree_dir: remove_tree: $!";
        mkdir $rmtree_dir
            or die "$rmtree_dir: mkdir: $!";
    }

    # Smoking section.
    chdir $smoker
        or die "$smoker: chdir: $!";

    # Rewrite config file. (So as to e-mail the committer.)
    open my $cfgin, "<", $cfgtmpl
        or die "$cfgtmpl: open(<): $!";
    open my $cfgout, ">", $cfg
        or die "$cfg: open(>): $!";
    while (<$cfgin>) {
        s/%COMMITTER_NAME%/$winner->{'cn'}/g;
        s/%COMMITTER_EMAIL%/$winner->{'ce'}/g;
        print $cfgout $_
            or die "$cfg: print: $!";
    }
    close $cfgout
        or warn "$cfg: close: $!";
    close $cfgin
        or warn "$cfgtmpl: close: $!";

    $ENV{'TEST_SMOKE_BRANCH'} = $winner->{'branch'};

    # Execute smoker.
    open my $logfh, ">", $log
        or die "$log: open(>): $!";
    my $pid = open3(\*STDIN, $logfh, $logfh, @cmd);
    msay "Started $describe";

    # Wait for the smoker to finish.
    waitpid $pid, 0;
    my $exit_status = $? >> 8;
    msay "Finished: $exit_status";
    close $logfh
        or warn "$log: close: $!";

    # Restore STDIN which gets whacked from the open3() call above.
    open STDIN, "<", $^O eq "MSWin32" ? "con:" : "/dev/tty"
        or warn "STDIN: open(<): $!"
        if ! -t STDIN;

    # Replace .git for working.
    move "$workdir/.git", "$worksrcdir/.git"
        or die "$workdir/.git -> $worksrcdir/.git: move: $!";

    # Compress *.log files to reduce download time.
    find({
            'no_chdir' => true,
            'wanted' => sub {
                my $file = $File::Find::name;
                if ($file =~ /\.log$/i) {
                    system $cmz_bin, $cmz_opt, $file;
                    if ($?) {
                        warn "$file: gzip: error status ", ($? >> 8), "\n";
                    }
                }
            },
        },
        "logs/",
    );

    # Publish reports on web site. (In case of network outage, it is nonfatal.)
    system $rsync, "-e", $ssh, "-az", "logs/", $log_dst;
    warn "rsync: $?"
        if $?;

    chdir $worksrcdir
        or die "$worksrcdir: chdir: $!";
}

exit 0;

sub remove_gitignore
{
    if ($File::Find::name =~ m{/\.gitignore$}i) {
        unlink $File::Find::name
            or die "$File::Find::name: unlink: $!";
    }
}

sub msay(@)
{
    for (split /\n/, join "", @_) {
        say substr(scalar localtime, 4), " [$o{'what'}] ", $_;
    }
}

sub nsay(@)
{
    for (split /\n/, join "", @_) {
        say "[$o{'what'}] ", $_;
    }
}
